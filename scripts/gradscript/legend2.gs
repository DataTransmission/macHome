***************************************************************************************
*       $Id: legend2.gs,v 1.3 2010/03/28 05:08:19 bguan Exp $
*       Copyright (C) 2009 Bin Guan.
*       Distributed under GNU/GPL.
***************************************************************************************
function legend2(arg)
*
* Display the legend for a plot generated by drawmark.gs.
*
rc=gsfallow('on')

if(arg='-h')
usage()
return
endif

tmpdir='/tmp'
whoamifile='./.whoami.bGASL'
'!whoami>'whoamifile
whoami=sublin(read(whoamifile),2)
rc=close(whoamifile)
'!unlink 'whoamifile
mytmpdir=tmpdir'/bGASL-'whoami
'!mkdir -p 'mytmpdir

*
* Read legend data.
*
flag=1
cnt=0
while(flag)
result=read(mytmpdir'/drawmark.txt~')
status=sublin(result,1)
if(status!=0)
flag=0
else
cnt=cnt+1
line.cnt=sublin(result,2)
endif
endwhile
num_var=cnt

*
* Initialize other options.
*
x=0
y=0
_.orientation.1='v'
_.position.1='tl'
_.xoffset.1=0
_.yoffset.1=0
_.scalefactor.1=1

*
* Parse -orient option (orientation).
*
rc=parseopt(arg,'-','orient','orientation')

*
* Parse -xo option (x offset).
*
rc=parseopt(arg,'-','xo','xoffset')

*
* Parse -yo option (y offset).
*
rc=parseopt(arg,'-','yo','yoffset')

*
* Parse -scale option (scaling factor).
*
rc=parseopt(arg,'-','scale','scalefactor')

*
* Get plot area
*
'query gxinfo'
line3=sublin(result,3)
line4=sublin(result,4)
x1=subwrd(line3,4)
x2=subwrd(line3,6)
y1=subwrd(line4,4)
y2=subwrd(line4,6)

*
* Define sizes and spacing.
*
size=0.11
line_length=0.55
small_spacing=0.11
'query pp2xy 0 0'
tmpxa=subwrd(result,3)
'query pp2xy 1 1'
tmpxb=subwrd(result,3)
rvratio=tmpxb-tmpxa
size=size*rvratio*_.scalefactor.1
line_length=line_length*math_sqrt(_.scalefactor.1)
small_spacing=small_spacing*rvratio*_.scalefactor.1

*
* Draw legend.
*
if(_.position.1='tl')
xx=x1+(0.5*size+small_spacing)
yy=y2-(0.5*size+small_spacing)
endif
xx=xx+_.xoffset.1
yy=yy+_.yoffset.1
x=xx
y=yy
cnt=1
while(cnt<=num_var)
mark=subwrd(line.cnt,1)
color=subwrd(line.cnt,2)
size=subwrd(line.cnt,3)
wrdcnt=4
text=subwrd(line.cnt,wrdcnt)
while(subwrd(line.cnt,wrdcnt)!='')
wrdcnt=wrdcnt+1
text=text' 'subwrd(line.cnt,wrdcnt)
endwhile
'query string 'text
string_width=subwrd(result,4)
if(_.orientation.1='h'&x+line_length+small_spacing+string_width>x2)
x=xx
y=y-(size+small_spacing)
endif
'set line 'color
'draw mark 'mark' 'x' 'y' 'size
'set string 1 l'
*'draw string 'x+size+small_spacing' 'y' 'text
'draw string 'x+0.1+small_spacing' 'y' 'text
if(_.orientation.1='v')
y=y-(size+small_spacing)
endif
if(_.orientation.1='h')
*x=x+(size+small_spacing+string_width+2*small_spacing)
x=x+(0.1+small_spacing+string_width+2*small_spacing)
endif
cnt=cnt+1
endwhile
'set string 1 bl'

return
***************************************************************************************
function usage()
*
* Print usage information.
*
say '  Display the legend for a plot generated by drawmark.gs.'
say ''
say '  Usage: legend2 [-orient v|h] [-xo <xoffset>] [-yo <yoffset>] [-scale <scalingfactor>]'
say '     -orient h: use for horizontal oriention. Default is vertical.'
say '     <xoffset>: Horizontal offset to default position (i.e., top-left). Defaults to 0.'
say '     <yoffset>: Vertical offset to default position (i.e., top-left). Defaults to 0.'
say '     <scalingfactor>: scaling factor for mark size. Will NOT affect string size (Use "set strsiz" for that). Defaults to 1.'
say ''
say '  "legend2 -h" prints this usage information.'
say ''
say '  Dependencies: parsestr.gsf, parseopt.gsf'
say ''
say '  See also: drawmark.gs'
say ''
say '  Copyright (C) 2009 Bin Guan.'
say '  Distributed under GNU/GPL.'
return
